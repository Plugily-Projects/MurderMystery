plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }


    maven {
        url = uri('https://papermc.io/repo/repository/maven-public/')
    }

    maven {
        url = uri('https://maven.plugily.xyz/releases')
    }

    maven {
        url = uri('https://maven.plugily.xyz/snapshots')
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.3-R0.1-SNAPSHOT'
    implementation('plugily.projects:MiniGamesBox-Classic:1.2.0-SNAPSHOT5') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'fr.mrmicky', module: 'FastInv'
        exclude group: 'me.tigerhix.lib', module: 'scoreboard'
        exclude group: 'com.github.cryptomorin', module: 'XSeries'
        exclude group: 'plugily.projects', module: 'MiniGamesBox-Inventory'
        exclude group: 'plugily.projects', module: 'MiniGamesBox-Database'
        exclude group: 'plugily.projects', module: 'MiniGamesBox-Utils'
        exclude group: 'io.papermc', module: 'paperlib'
    }
    compileOnly 'org.jetbrains:annotations:23.1.0'
    compileOnly files('lib/CorpseReborn.jar')
}


group = 'plugily.projects'
version = '1.7.9-SNAPSHOT39'
description = 'MurderMystery'

java {
    withJavadocJar()
}

build {
    dependsOn(shadowJar)
}

javadoc {
    options.encoding = 'UTF-8'
}

processResources {
    filesMatching("**/plugin.yml") {
        expand(project.properties)
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'plugily.projects.minigamesbox', 'plugily.projects.murdermystery.minigamesbox'
    relocate 'com.zaxxer.hikari', 'plugily.projects.murdermystery.database.hikari'
    minimize()
}

publishing {
    repositories {
        maven {
            name = "Releases"
            url = "https://maven.plugily.xyz/releases"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "Snapshots"
            url = uri("https://maven.plugily.xyz/snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
